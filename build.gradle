buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/palantir/releases/' }
    }

    dependencies {
        classpath 'com.palantir.gradle.conjure:gradle-conjure:4.27.2'
    }
}

plugins {
    id 'ru.vyarus.use-python' version '2.2.0'
    id "com.github.node-gradle.node" version "2.2.4"
}

allprojects {
    repositories {
        maven { url 'https://dl.bintray.com/palantir/releases/' }
    }
}

task install{
}
task check{
}
task build{

}

python.installVirtualenv = true
python.envPath = "venv"

python{
	pip "poetry:1.1.4"
}

task copyConjurePythonModels {
    dependsOn ':cato-api-models:compileConjurePython'
    doLast {
        copy {
            from "${rootDir}/cato-api-models/cato-api-models-python/python/cato_api_models"
            into "${rootDir}/cato_api_models"
        }
    }
}

task poetryInstall(type: PythonTask) {
	dependsOn 'pipInstall'
	dependsOn 'copyConjurePythonModels'
    command = "-m poetry install"
    inputs.file('pyproject.toml')
    inputs.file('poetry.lock')
    outputs.file('poetryInstallOutput')
}

task installOiio(type: PythonTask){
    command = "-m pip install -r oiio-requirements.txt --no-dependencies"
    inputs.file('oiio-requirements.txt')
    outputs.dir('venv/Lib/site-packages/oiio')
}

task pythonInstall{
    dependsOn 'poetryInstall'
    dependsOn 'installOiio'
}

task pytest(type: PythonTask){
    dependsOn 'pythonInstall'
    command = "-m pytest tests"
    inputs.dir('cato')
    inputs.dir('cato_server')
    inputs.dir('cato_api_client')
    inputs.dir('tests')
    outputs.dir('pytestOutput')
}

task copyFrontendToBuild {
    dependsOn(':frontend:yarnBuild')
    doLast {
        copy {
            from "${rootDir}/frontend/build"
            into "${rootDir}/cato_server/static"
        }
    }
}

task poetryBuild(type:Exec){
    dependsOn 'pythonInstall'
    dependsOn 'pytest'
    dependsOn 'copyFrontendToBuild'
    commandLine 'runInVenv.bat', 'poetry', 'build'
    inputs.dir('cato')
    inputs.dir('cato_server')
    inputs.file('pyproject.toml')
    inputs.file('poetry.lock')
    outputs.dir('dist')
}

install.dependsOn('pythonInstall')
check.dependsOn('pytest')
build.dependsOn('poetryBuild')

install.dependsOn(':frontend:yarnInstallDependencies')

task formatPython(type:Exec){
    commandLine 'venv/Scripts/black', 'cato', 'cato_server', 'tests', 'cato_api_client', 'alembic'
}

task lintPython(type:Exec){
    commandLine 'venv/Scripts/flake8', 'cato', 'cato_server', 'cato_api_client'
}

task format{
    dependsOn 'formatPython'
    dependsOn ':frontend:formatJs'
}

task lint{
    dependsOn 'lintPython'
}